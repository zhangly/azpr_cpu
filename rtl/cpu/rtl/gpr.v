/* 
 -- ============================================================================
 -- FILE NAME	: gpr.v
 -- DESCRIPTION : 用レジスタ
 -- ----------------------------------------------------------------------------
 -- Revision  Date		  Coding_by	 Comment
 -- 1.0.0	  2011/06/27  suito		 新作成
 -- ============================================================================
*/

/********** 共通ヘッダファイル **********/
`include "nettype.h"
`include "global_config.h"
`include "stddef.h"

/********** eヘッダファイル **********/
`include "cpu.h"

/********** モジュ`ル **********/
module gpr (
	/********** クロック & リセット **********/
	input  wire				   clk,				   // クロック
	input  wire				   reset,			   // 非同期リセット
	/********** iみ出しポ`ト 0 **********/
	input  wire [`RegAddrBus]  rd_addr_0,		   // iみ出しアドレス
	output wire [`WordDataBus] rd_data_0,		   // iみ出しデ`タ
	/********** iみ出しポ`ト 1 **********/
	input  wire [`RegAddrBus]  rd_addr_1,		   // iみ出しアドレス
	output wire [`WordDataBus] rd_data_1,		   // iみ出しデ`タ
	/********** きzみポ`ト **********/
	input  wire				   we_,				   // きzみ有
	input  wire [`RegAddrBus]  wr_addr,			   // きzみアドレス
	input  wire [`WordDataBus] wr_data			   // きzみデ`タ
);

	/********** 内部信号 **********/
	reg [`WordDataBus]		   gpr [`REG_NUM-1:0]; // レジスタ配列
	integer					   i;				   // イテレ`タ

	/********** iみ出しアクセス (Write After Read) **********/
	// iみ出しポ`ト 0
	assign rd_data_0 = ((we_ == `ENABLE_) && (wr_addr == rd_addr_0)) ? 
					   wr_data : gpr[rd_addr_0];
	// iみ出しポ`ト 1
	assign rd_data_1 = ((we_ == `ENABLE_) && (wr_addr == rd_addr_1)) ? 
					   wr_data : gpr[rd_addr_1];
   
	/********** きzみアクセス **********/
	always @ (posedge clk or `RESET_EDGE reset) begin
		if (reset == `RESET_ENABLE) begin 
			/* 非同期リセット */
			for (i = 0; i < `REG_NUM; i = i + 1) begin
				gpr[i]		 <= #1 `WORD_DATA_W'h0;
			end
		end else begin
			/* きzみアクセス */
			if (we_ == `ENABLE_) begin 
				gpr[wr_addr] <= #1 wr_data;
			end
		end
	end

endmodule 
