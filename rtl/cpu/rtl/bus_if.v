/*
 -- ============================================================================
 -- FILE NAME	: bus_if.v
 -- DESCRIPTION : バスインタフェ`ス
 -- ----------------------------------------------------------------------------
 -- Revision  Date		  Coding_by	 Comment
 -- 1.0.0	  2011/06/27  suito		 新作成
 -- ============================================================================
*/

/********** 共通ヘッダファイル **********/
`include "nettype.h"
`include "global_config.h"
`include "stddef.h"

/********** eヘッダファイル **********/
`include "cpu.h"
`include "bus.h"

/********** モジュ`ル **********/
module bus_if (
	/********** クロック & リセット **********/
	input  wire				   clk,			   // クロック
	input  wire				   reset,		   // 非同期リセット
	/********** パイプライン制御信号 **********/
	input  wire				   stall,		   // スト`ル
	input  wire				   flush,		   // フラッシュ信号
	output reg				   busy,		   // ビジ`信号
	/********** CPUインタフェ`ス **********/
	input  wire [`WordAddrBus] addr,		   // アドレス
	input  wire				   as_,			   // アドレス有
	input  wire				   rw,			   // iみ／き
	input  wire [`WordDataBus] wr_data,		   // きzみデ`タ
	output reg	[`WordDataBus] rd_data,		   // iみ出しデ`タ
	/********** SPMインタフェ`ス **********/
	input  wire [`WordDataBus] spm_rd_data,	   // iみ出しデ`タ
	output wire [`WordAddrBus] spm_addr,	   // アドレス
	output reg				   spm_as_,		   // アドレスストロ`ブ
	output wire				   spm_rw,		   // iみ／き
	output wire [`WordDataBus] spm_wr_data,	   // きzみデ`タ
	/********** バスインタフェ`ス **********/
	input  wire [`WordDataBus] bus_rd_data,	   // iみ出しデ`タ
	input  wire				   bus_rdy_,	   // レディ
	input  wire				   bus_grnt_,	   // バスグラント
	output reg				   bus_req_,	   // バスリクエスト
	output reg	[`WordAddrBus] bus_addr,	   // アドレス
	output reg				   bus_as_,		   // アドレスストロ`ブ
	output reg				   bus_rw,		   // iみ／き
	output reg	[`WordDataBus] bus_wr_data	   // きzみデ`タ
);

	/********** 内部信号 **********/
	reg	 [`BusIfStateBus]	   state;		   // バスインタフェ`スの状B
	reg	 [`WordDataBus]		   rd_buf;		   // iみ出しバッファ
	wire [`BusSlaveIndexBus]   s_index;		   // バススレ`ブインデックス

	/********** バススレ`ブのインデックス **********/
	assign s_index	   = addr[`BusSlaveIndexLoc];

	/********** 出力のアサイン **********/
	assign spm_addr	   = addr;
	assign spm_rw	   = rw;
	assign spm_wr_data = wr_data;
						 
	/********** メモリアクセスの制御 **********/
	always @(*) begin
		/* デフォルト */
		rd_data	 = `WORD_DATA_W'h0;
		spm_as_	 = `DISABLE_;
		busy	 = `DISABLE;
		/* バスインタフェ`スの状B */
		case (state)
			`BUS_IF_STATE_IDLE	 : begin // アイドル
				/* メモリアクセス */
				if ((flush == `DISABLE) && (as_ == `ENABLE_)) begin
					/* アクセス先のxk */
					if (s_index == `BUS_SLAVE_1) begin // SPMへアクセス
						if (stall == `DISABLE) begin // スト`ルk生のチェック
							spm_as_	 = `ENABLE_;
							if (rw == `READ) begin // iみ出しアクセス
								rd_data	 = spm_rd_data;
							end
						end
					end else begin					   // バスへアクセス
						busy	 = `ENABLE;
					end
				end
			end
			`BUS_IF_STATE_REQ	 : begin // バスリクエスト
				busy	 = `ENABLE;
			end
			`BUS_IF_STATE_ACCESS : begin // バスアクセス
				/* レディ待ち */
				if (bus_rdy_ == `ENABLE_) begin // レディ到着
					if (rw == `READ) begin // iみ出しアクセス
						rd_data	 = bus_rd_data;
					end
				end else begin					// レディ未到着
					busy	 = `ENABLE;
				end
			end
			`BUS_IF_STATE_STALL	 : begin // スト`ル
				if (rw == `READ) begin // iみ出しアクセス
					rd_data	 = rd_buf;
				end
			end
		endcase
	end

   /********** バスインタフェ`スの状B制御 **********/ 
   always @(posedge clk or `RESET_EDGE reset) begin
		if (reset == `RESET_ENABLE) begin
			/* 非同期リセット */
			state		<= #1 `BUS_IF_STATE_IDLE;
			bus_req_	<= #1 `DISABLE_;
			bus_addr	<= #1 `WORD_ADDR_W'h0;
			bus_as_		<= #1 `DISABLE_;
			bus_rw		<= #1 `READ;
			bus_wr_data <= #1 `WORD_DATA_W'h0;
			rd_buf		<= #1 `WORD_DATA_W'h0;
		end else begin
			/* バスインタフェ`スの状B */
			case (state)
				`BUS_IF_STATE_IDLE	 : begin // アイドル
					/* メモリアクセス */
					if ((flush == `DISABLE) && (as_ == `ENABLE_)) begin 
						/* アクセス先のxk */
						if (s_index != `BUS_SLAVE_1) begin // バスへアクセス
							state		<= #1 `BUS_IF_STATE_REQ;
							bus_req_	<= #1 `ENABLE_;
							bus_addr	<= #1 addr;
							bus_rw		<= #1 rw;
							bus_wr_data <= #1 wr_data;
						end
					end
				end
				`BUS_IF_STATE_REQ	 : begin // バスリクエスト
					/* バスグラント待ち */
					if (bus_grnt_ == `ENABLE_) begin // バス孬@得
						state		<= #1 `BUS_IF_STATE_ACCESS;
						bus_as_		<= #1 `ENABLE_;
					end
				end
				`BUS_IF_STATE_ACCESS : begin // バスアクセス
					/* アドレスストロ`ブのネゲ`ト */
					bus_as_		<= #1 `DISABLE_;
					/* レディ待ち */
					if (bus_rdy_ == `ENABLE_) begin // レディ到着
						bus_req_	<= #1 `DISABLE_;
						bus_addr	<= #1 `WORD_ADDR_W'h0;
						bus_rw		<= #1 `READ;
						bus_wr_data <= #1 `WORD_DATA_W'h0;
						/* iみ出しデ`タの保存 */
						if (bus_rw == `READ) begin // iみ出しアクセス
							rd_buf		<= #1 bus_rd_data;
						end
						/* スト`ルk生のチェック */
						if (stall == `ENABLE) begin // スト`ルk生
							state		<= #1 `BUS_IF_STATE_STALL;
						end else begin				// スト`ル未k生
							state		<= #1 `BUS_IF_STATE_IDLE;
						end
					end
				end
				`BUS_IF_STATE_STALL	 : begin // スト`ル
					/* スト`ルk生のチェック */
					if (stall == `DISABLE) begin // スト`ル解除
						state		<= #1 `BUS_IF_STATE_IDLE;
					end
				end
			endcase
		end
	end

endmodule
