/*
 -- ============================================================================
 -- FILE NAME	: decoder.v
 -- DESCRIPTION : 命令デコ`ダ
 -- ----------------------------------------------------------------------------
 -- Revision  Date		  Coding_by	 Comment
 -- 1.0.0	  2011/06/27  suito		 新作成
 -- ============================================================================
*/

/********** 共通ヘッダファイル **********/
`include "nettype.h"
`include "global_config.h"
`include "stddef.h"

/********** eヘッダファイル **********/
`include "isa.h"
`include "cpu.h"

/********** モジュ`ル **********/
module decoder (
	/********** IF/IDパイプラインレジスタ **********/
	input  wire [`WordAddrBus]	 if_pc,			 // プログラムカウンタ
	input  wire [`WordDataBus]	 if_insn,		 // 命令
	input  wire					 if_en,			 // パイプラインデ`タの有
	/********** GPRインタフェ`ス **********/
	input  wire [`WordDataBus]	 gpr_rd_data_0, // iみ出しデ`タ 0
	input  wire [`WordDataBus]	 gpr_rd_data_1, // iみ出しデ`タ 1
	output wire [`RegAddrBus]	 gpr_rd_addr_0, // iみ出しアドレス 0
	output wire [`RegAddrBus]	 gpr_rd_addr_1, // iみ出しアドレス 1
	/********** フォワ`ディング **********/
	// IDステ`ジからのフォワ`ディング
	input  wire					 id_en,			// パイプラインデ`タの有
	input  wire [`RegAddrBus]	 id_dst_addr,	// きzみアドレス
	input  wire					 id_gpr_we_,	// きzみ有
	input  wire [`MemOpBus]		 id_mem_op,		// メモリオペレ`ション
	// EXステ`ジからのフォワ`ディング
	input  wire					 ex_en,			// パイプラインデ`タの有
	input  wire [`RegAddrBus]	 ex_dst_addr,	// きzみアドレス
	input  wire					 ex_gpr_we_,	// きzみ有
	input  wire [`WordDataBus]	 ex_fwd_data,	// フォワ`ディングデ`タ
	// MEMステ`ジからのフォワ`ディング
	input  wire [`WordDataBus]	 mem_fwd_data,	// フォワ`ディングデ`タ
	/********** 制御レジスタインタフェ`ス **********/
	input  wire [`CpuExeModeBus] exe_mode,		// g行モ`ド
	input  wire [`WordDataBus]	 creg_rd_data,	// iみ出しデ`タ
	output wire [`RegAddrBus]	 creg_rd_addr,	// iみ出しアドレス
	/********** デコ`ドY果 **********/
	output reg	[`AluOpBus]		 alu_op,		// ALUオペレ`ション
	output reg	[`WordDataBus]	 alu_in_0,		// ALU入力 0
	output reg	[`WordDataBus]	 alu_in_1,		// ALU入力 1
	output reg	[`WordAddrBus]	 br_addr,		// 分岐アドレス
	output reg					 br_taken,		// 分岐の成立
	output reg					 br_flag,		// 分岐フラグ
	output reg	[`MemOpBus]		 mem_op,		// メモリオペレ`ション
	output wire [`WordDataBus]	 mem_wr_data,	// メモリきzみデ`タ
	output reg	[`CtrlOpBus]	 ctrl_op,		// 制御オペレ`ション
	output reg	[`RegAddrBus]	 dst_addr,		// 用レジスタきzみアドレス
	output reg					 gpr_we_,		// 用レジスタきzみ有
	output reg	[`IsaExpBus]	 exp_code,		// 例外コ`ド
	output reg					 ld_hazard		// ロ`ドハザ`ド
);

	/********** 命令フィ`ルド **********/
	wire [`IsaOpBus]	op		= if_insn[`IsaOpLoc];	  // オペコ`ド
	wire [`RegAddrBus]	ra_addr = if_insn[`IsaRaAddrLoc]; // Raアドレス
	wire [`RegAddrBus]	rb_addr = if_insn[`IsaRbAddrLoc]; // Rbアドレス
	wire [`RegAddrBus]	rc_addr = if_insn[`IsaRcAddrLoc]; // Rcアドレス
	wire [`IsaImmBus]	imm		= if_insn[`IsaImmLoc];	  // 即
	/********** 即 **********/
	// 符号
	wire [`WordDataBus] imm_s = {{`ISA_EXT_W{imm[`ISA_IMM_MSB]}}, imm};
	// ゼロ
	wire [`WordDataBus] imm_u = {{`ISA_EXT_W{1'b0}}, imm};
	/********** レジスタのiみ出しアドレス **********/
	assign gpr_rd_addr_0 = ra_addr; // 用レジスタiみ出しアドレス 0
	assign gpr_rd_addr_1 = rb_addr; // 用レジスタiみ出しアドレス 1
	assign creg_rd_addr	 = ra_addr; // 制御レジスタiみ出しアドレス
	/********** 用レジスタのiみ出しデ`タ **********/
	reg			[`WordDataBus]	ra_data;						  // 符号なしRa
	wire signed [`WordDataBus]	s_ra_data = $signed(ra_data);	  // 符号付きRa
	reg			[`WordDataBus]	rb_data;						  // 符号なしRb
	wire signed [`WordDataBus]	s_rb_data = $signed(rb_data);	  // 符号付きRb
	assign mem_wr_data = rb_data; // メモリきzみデ`タ
	/********** アドレス **********/
	wire [`WordAddrBus] ret_addr  = if_pc + 1'b1;					 // り番地
	wire [`WordAddrBus] br_target = if_pc + imm_s[`WORD_ADDR_MSB:0]; // 分岐先
	wire [`WordAddrBus] jr_target = ra_data[`WordAddrLoc];		   // ジャンプ先

	/********** フォワ`ディング **********/
	always @(*) begin
		/* Raレジスタ */
		if ((id_en == `ENABLE) && (id_gpr_we_ == `ENABLE_) && 
			(id_dst_addr == ra_addr)) begin
			ra_data = ex_fwd_data;	 // EXステ`ジからのフォワ`ディング
		end else if ((ex_en == `ENABLE) && (ex_gpr_we_ == `ENABLE_) && 
					 (ex_dst_addr == ra_addr)) begin
			ra_data = mem_fwd_data;	 // MEMステ`ジからのフォワ`ディング
		end else begin
			ra_data = gpr_rd_data_0; // レジスタファイルからのiみ出し
		end
		/* Rbレジスタ */
		if ((id_en == `ENABLE) && (id_gpr_we_ == `ENABLE_) && 
			(id_dst_addr == rb_addr)) begin
			rb_data = ex_fwd_data;	 // EXステ`ジからのフォワ`ディング
		end else if ((ex_en == `ENABLE) && (ex_gpr_we_ == `ENABLE_) && 
					 (ex_dst_addr == rb_addr)) begin
			rb_data = mem_fwd_data;	 // MEMステ`ジからのフォワ`ディング
		end else begin
			rb_data = gpr_rd_data_1; // レジスタファイルからのiみ出し
		end
	end

	/********** ロ`ドハザ`ドの食 **********/
	always @(*) begin
		if ((id_en == `ENABLE) && (id_mem_op == `MEM_OP_LDW) &&
			((id_dst_addr == ra_addr) || (id_dst_addr == rb_addr))) begin
			ld_hazard = `ENABLE;  // ロ`ドハザ`ド
		end else begin
			ld_hazard = `DISABLE; // ハザ`ドなし
		end
	end

	/********** 命令のデコ`ド **********/
	always @(*) begin
		/* デフォルト */
		alu_op	 = `ALU_OP_NOP;
		alu_in_0 = ra_data;
		alu_in_1 = rb_data;
		br_taken = `DISABLE;
		br_flag	 = `DISABLE;
		br_addr	 = {`WORD_ADDR_W{1'b0}};
		mem_op	 = `MEM_OP_NOP;
		ctrl_op	 = `CTRL_OP_NOP;
		dst_addr = rb_addr;
		gpr_we_	 = `DISABLE_;
		exp_code = `ISA_EXP_NO_EXP;
		/* オペコ`ドの判定 */
		if (if_en == `ENABLE) begin
			case (op)
				/* 理演算命令 */
				`ISA_OP_ANDR  : begin // レジスタ同士の理e
					alu_op	 = `ALU_OP_AND;
					dst_addr = rc_addr;
					gpr_we_	 = `ENABLE_;
				end
				`ISA_OP_ANDI  : begin // レジスタと即の理e
					alu_op	 = `ALU_OP_AND;
					alu_in_1 = imm_u;
					gpr_we_	 = `ENABLE_;
				end
				`ISA_OP_ORR	  : begin // レジスタ同士の理和
					alu_op	 = `ALU_OP_OR;
					dst_addr = rc_addr;
					gpr_we_	 = `ENABLE_;
				end
				`ISA_OP_ORI	  : begin // レジスタと即の理和
					alu_op	 = `ALU_OP_OR;
					alu_in_1 = imm_u;
					gpr_we_	 = `ENABLE_;
				end
				`ISA_OP_XORR  : begin // レジスタ同士の排他的理和
					alu_op	 = `ALU_OP_XOR;
					dst_addr = rc_addr;
					gpr_we_	 = `ENABLE_;
				end
				`ISA_OP_XORI  : begin // レジスタと即の排他的理和
					alu_op	 = `ALU_OP_XOR;
					alu_in_1 = imm_u;
					gpr_we_	 = `ENABLE_;
				end
				/* 算g演算命令 */
				`ISA_OP_ADDSR : begin // レジスタ同士の符号付き加算
					alu_op	 = `ALU_OP_ADDS;
					dst_addr = rc_addr;
					gpr_we_	 = `ENABLE_;
				end
				`ISA_OP_ADDSI : begin // レジスタと即の符号付き加算
					alu_op	 = `ALU_OP_ADDS;
					alu_in_1 = imm_s;
					gpr_we_	 = `ENABLE_;
				end
				`ISA_OP_ADDUR : begin // レジスタ同士の符号なし加算
					alu_op	 = `ALU_OP_ADDU;
					dst_addr = rc_addr;
					gpr_we_	 = `ENABLE_;
				end
				`ISA_OP_ADDUI : begin // レジスタと即の符号なし加算
					alu_op	 = `ALU_OP_ADDU;
					alu_in_1 = imm_s;
					gpr_we_	 = `ENABLE_;
				end
				`ISA_OP_SUBSR : begin // レジスタ同士の符号付きp算
					alu_op	 = `ALU_OP_SUBS;
					dst_addr = rc_addr;
					gpr_we_	 = `ENABLE_;
				end
				`ISA_OP_SUBUR : begin // レジスタ同士の符号なしp算
					alu_op	 = `ALU_OP_SUBU;
					dst_addr = rc_addr;
					gpr_we_	 = `ENABLE_;
				end
				/* シフト命令 */
				`ISA_OP_SHRLR : begin // レジスタ同士の理右シフト
					alu_op	 = `ALU_OP_SHRL;
					dst_addr = rc_addr;
					gpr_we_	 = `ENABLE_;
				end
				`ISA_OP_SHRLI : begin // レジスタと即の理右シフト
					alu_op	 = `ALU_OP_SHRL;
					alu_in_1 = imm_u;
					gpr_we_	 = `ENABLE_;
				end
				`ISA_OP_SHLLR : begin // レジスタ同士の理左シフト
					alu_op	 = `ALU_OP_SHLL;
					dst_addr = rc_addr;
					gpr_we_	 = `ENABLE_;
				end
				`ISA_OP_SHLLI : begin // レジスタと即の理左シフト
					alu_op	 = `ALU_OP_SHLL;
					alu_in_1 = imm_u;
					gpr_we_	 = `ENABLE_;
				end
				/* 分岐命令 */
				`ISA_OP_BE	  : begin // レジスタ同士の符号付き比^（Ra == Rb）
					br_addr	 = br_target;
					br_taken = (ra_data == rb_data) ? `ENABLE : `DISABLE;
					br_flag	 = `ENABLE;
				end
				`ISA_OP_BNE	  : begin // レジスタ同士の符号付き比^（Ra != Rb）
					br_addr	 = br_target;
					br_taken = (ra_data != rb_data) ? `ENABLE : `DISABLE;
					br_flag	 = `ENABLE;
				end
				`ISA_OP_BSGT  : begin // レジスタ同士の符号付き比^（Ra < Rb）
					br_addr	 = br_target;
					br_taken = (s_ra_data < s_rb_data) ? `ENABLE : `DISABLE;
					br_flag	 = `ENABLE;
				end
				`ISA_OP_BUGT  : begin // レジスタ同士の符号なし比^（Ra < Rb）
					br_addr	 = br_target;
					br_taken = (ra_data < rb_data) ? `ENABLE : `DISABLE;
					br_flag	 = `ENABLE;
				end
				`ISA_OP_JMP	  : begin // o条件分岐
					br_addr	 = jr_target;
					br_taken = `ENABLE;
					br_flag	 = `ENABLE;
				end
				`ISA_OP_CALL  : begin // コ`ル
					alu_in_0 = {ret_addr, {`BYTE_OFFSET_W{1'b0}}};
					br_addr	 = jr_target;
					br_taken = `ENABLE;
					br_flag	 = `ENABLE;
					dst_addr = `REG_ADDR_W'd31;
					gpr_we_	 = `ENABLE_;
				end
				/* メモリアクセス命令 */
				`ISA_OP_LDW	  : begin // ワ`ドiみ出し
					alu_op	 = `ALU_OP_ADDU;
					alu_in_1 = imm_s;
					mem_op	 = `MEM_OP_LDW;
					gpr_we_	 = `ENABLE_;
				end
				`ISA_OP_STW	  : begin // ワ`ドきzみ
					alu_op	 = `ALU_OP_ADDU;
					alu_in_1 = imm_s;
					mem_op	 = `MEM_OP_STW;
				end
				/* システムコ`ル命令 */
				`ISA_OP_TRAP  : begin // トラップ
					exp_code = `ISA_EXP_TRAP;
				end
				/* 特孛令 */
				`ISA_OP_RDCR  : begin // 制御レジスタのiみ出し
					if (exe_mode == `CPU_KERNEL_MODE) begin
						alu_in_0 = creg_rd_data;
						gpr_we_	 = `ENABLE_;
					end else begin
						exp_code = `ISA_EXP_PRV_VIO;
					end
				end
				`ISA_OP_WRCR  : begin // 制御レジスタへのきzみ
					if (exe_mode == `CPU_KERNEL_MODE) begin
						ctrl_op	 = `CTRL_OP_WRCR;
					end else begin
						exp_code = `ISA_EXP_PRV_VIO;
					end
				end
				`ISA_OP_EXRT  : begin // 例外からの
					if (exe_mode == `CPU_KERNEL_MODE) begin
						ctrl_op	 = `CTRL_OP_EXRT;
					end else begin
						exp_code = `ISA_EXP_PRV_VIO;
					end
				end
				/* その他の命令 */
				default		  : begin // 未定x命令
					exp_code = `ISA_EXP_UNDEF_INSN;
				end
			endcase
		end
	end

endmodule
